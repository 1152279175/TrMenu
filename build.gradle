import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'io.izzel.taboolib' version '1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
}

group 'me.arasple.mc.trmenu'
version '2.04-DEV'

taboolib {
    tabooLibVersion = '5.35'
    loaderVersion = '2.6'
    classifier = null

    relocate 'net.wesjd', project.group + '.modules.inputer.anvil'
}

task buildJar(dependsOn: [clean, jar])

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(
                'name': project.name,
                'main': project.group + ".boot.PluginBoot",
                'version': project.version,
                'taboolib': taboolib.tabooLibVersion,
                'built': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss").format(new Date()),
                'builder': System.properties['user.name'],
                'os': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://rayzr.dev/repo/' }
    maven { url "http://repo.dakanilabs.com/repository/public/" }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib'
    compile 'ink.ptms.core:v11600:11600:all'
    compile 'me.clip:placeholderapi:2.10.9'
    compile 'ink.ptms.cronus:Cronus:1.1.0.2019.8.31-SNAPSHOT-4'
    taboo 'com.github.WesJD.AnvilGUI:anvilgui:-SNAPSHOT'
    taboo 'org.bstats:bstats-bukkit:1.7'
    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}