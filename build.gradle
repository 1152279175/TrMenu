import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4-M2'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        languageVersion = "1.4"
    }
}

group 'me.arasple.mc.trmenu'
version '2.0-BETA'

task buildJar(dependsOn: [clean, shadowJar])

shadowJar {
    dependencies {
        include(dependency('org.bstats:bstats-bukkit:1.7'))
        include(dependency('io.izzel.taboolib:TabooLibLoader:2.1:all'))
    }
    relocate 'io.izzel.taboolib.loader', project.group + ".boot"
    relocate "org.bstats.bukkit", project.group + '.modules.metrics'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(
                'name': project.name,
                'main': project.group + ".boot.PluginTransfer",
                'mainTransfer': project.group + "." + project.name,
                'version': project.version + "_" + new SimpleDateFormat("yyyy-MM-dd' 'HH:mm").format(new Date()),
                'built': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'builder': System.properties['user.name'],
                'os': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    // SoftDepend Plugins
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "http://repo.dakanilabs.com/repository/public/" }
    maven { url "http://repo.ptms.ink/repository/codemc-nms/" }
    maven { url "http://repo.ptms.ink/repository/maven-releases/" }
    // Reop
    maven { url "https://jitpack.io" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4-M2"
    compile 'org.spigotmc:spigot:1.16.1-R0.1-20200718.075154-22'
    compile 'org.bstats:bstats-bukkit:1.7'
    compile 'io.izzel.taboolib:TabooLib:5.3:all'
    compile 'io.izzel.taboolib:TabooLibLoader:2.1:all'
    compile 'me.clip:placeholderapi:2.10.5'
    compile 'com.github.shynixn.headdatabase:hdb-api:1.0'
    compile 'ink.ptms.cronus:Cronus:1.1.0.2019.8.31-SNAPSHOT-4'
    shadow fileTree(dir: 'libs', includes: ['*.jar'])
}