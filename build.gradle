import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group 'me.arasple.mc.trmenu'
version '2.0'

task buildJar(dependsOn: [clean, shadowJar])

shadowJar {
    dependencies {
        include(dependency('org.bstats:bstats-bukkit:1.7'))
        include(dependency('io.izzel.taboolib:TabooLibLoader:2.1:all'))
    }
    relocate 'io.izzel.taboolib.loader', project.group + ".boot"
    relocate "org.bstats.bukkit", project.group + '.modules.metrics'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(
                'name': project.name,
                'main': project.group + ".boot.PluginTransfer",
                'mainTransfer': project.group + "." + project.name,
                'version': project.version,
                'built': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'builder': System.properties['user.name'],
                'os': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "http://repo.ptms.ink/repository/codemc-nms/" }
    maven { url "http://repo.ptms.ink/repository/maven-releases/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "http://repo.dakanilabs.com/repository/public/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.3.72"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
    compile 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit:1.7'
    compile 'io.izzel.taboolib:TabooLib:5.28:all'
    compile 'io.izzel.taboolib:TabooLibLoader:2.1:all'
    compile 'me.clip:placeholderapi:2.10.5'
    compile 'com.github.shynixn.headdatabase:hdb-api:1.0'

    shadow fileTree(dir: 'libs', includes: ['*.jar'])
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}