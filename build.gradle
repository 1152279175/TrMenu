import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0-rc'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group 'me.arasple.mc.trmenu'
version '2.02'

task buildJar(dependsOn: [clean, shadowJar])

shadowJar {
    dependencies {
        [
                'org.bstats:bstats-bukkit:1.7',
                'io.izzel.taboolib:TabooLibLoader:2.2:all',
                'com.github.WesJD.AnvilGUI:anvilgui:-SNAPSHOT'
        ].forEach {
            include(dependency(it))
        }
    }
    relocate 'io.izzel.taboolib.loader', project.group + '.boot'
    relocate 'net.wesjd', project.group + '.modules.inputer.anvil'
    relocate 'org.bstats.bukkit', project.group + '.modules.metrics'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml', 'bungee.yml'
        expand(
                'name': project.name,
                'main': project.group + ".boot.PluginTransfer",
                'mainBungee': project.group + ".TrMenuBungee",
                'mainTransfer': project.group + "." + project.name,
                'version': project.version,
                'built': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'builder': System.properties['user.name'],
                'os': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    // Ptms Mirror
    maven { url 'http://repo.ptms.ink/repository/codemc-nms/' }
    maven { url 'http://repo.ptms.ink/repository/maven-releases/' }
    // Reop
    maven { url 'https://jitpack.io' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    // SoftDepend Plugins
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://rayzr.dev/repo/' }
    maven { url "http://repo.dakanilabs.com/repository/public/" }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib'
    compile 'org.spigotmc:spigot:1.16.1-R0.1-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit:1.7'
    compile 'io.izzel.taboolib:TabooLib:5.34:all'
    compile 'io.izzel.taboolib:TabooLibLoader:2.2:all'
    compile 'com.github.WesJD.AnvilGUI:anvilgui:-SNAPSHOT'
    compile 'me.clip:placeholderapi:2.10.7'
    compile 'ink.ptms.cronus:Cronus:1.1.0.2019.8.31-SNAPSHOT-4'
    shadow fileTree(dir: 'libs', includes: ['*.jar'])
}