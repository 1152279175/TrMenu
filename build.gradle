import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4-M2'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        languageVersion = "1.4"
    }
}

group 'me.arasple.mc.trmenu'
version '2.0'

task buildJar(dependsOn: [clean, shadowJar])

shadowJar {
    dependencies {
        [
                'org.bstats:bstats-bukkit:1.7',
                'io.izzel.taboolib:TabooLibLoader:2.1:all',
                'com.github.WesJD.AnvilGUI:anvilgui:-SNAPSHOT'
//                'org.jetbrains.kotlin:kotlin-stdlib-common:1.4-M2',
//                'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4-M2',
//                'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4-M2',
//                'org.jetbrains.kotlin:kotlin-stdlib:1.4-M2'
        ].forEach {
            include(dependency(it))
        }

    }
    relocate 'io.izzel.taboolib.loader', project.group + ".boot"
    relocate 'net.wesjd', project.group + ".modules.inputer.anvil"
    relocate "org.bstats.bukkit", project.group + '.modules.metrics'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml', 'bungee.yml'
        expand(
                'name': project.name,
                'main': project.group + ".boot.PluginTransfer",
                'mainBungee': project.group + ".TrMenuBungee",
                'mainTransfer': project.group + "." + project.name,
                'version': project.version,
                'built': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'builder': System.properties['user.name'],
                'os': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

repositories {
    // SoftDepend Plugins
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://rayzr.dev/repo/" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "http://repo.dakanilabs.com/repository/public/" }
    maven { url "http://repo.ptms.ink/repository/codemc-nms/" }
    maven { url "http://repo.ptms.ink/repository/maven-releases/" }
    // Reop
    maven { url "https://jitpack.io" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4-M2"
    compile 'org.spigotmc:spigot:1.16.1-R0.1-20200718.075154-22'
    compile 'org.bstats:bstats-bukkit:1.7'
    compile 'io.izzel.taboolib:TabooLib:5.31:all'
    compile 'io.izzel.taboolib:TabooLibLoader:2.1:all'
    compile 'com.github.WesJD.AnvilGUI:anvilgui:-SNAPSHOT'
    compile 'me.clip:placeholderapi:2.10.7'
    compile 'com.github.shynixn.headdatabase:hdb-api:1.0'
    compile 'ink.ptms.cronus:Cronus:1.1.0.2019.8.31-SNAPSHOT-4'
    shadow fileTree(dir: 'libs', includes: ['*.jar'])
}